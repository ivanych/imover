#!/usr/bin/perl

#
# imover - cкрипт для переименования и сортировки фото и видеофайлов по дате съемки
# Михаил ivanych Иванов <m.ivanych@gmail.com>
#

use strict;
use warnings;

use Getopt::Long;

use Imover;

# Модели устройств
my $model = 'iphone|s2|s1|desire|a480|i9300|p690';

# Флаги копирования, удаления и вывода сообщений о неподдерживаемых типах
my ($opt_c, $opt_d, $opt_a);

# Корневой каталог для загрузки файлов
my $opt_root = $ENV{HOME} . '/imover';

# Читаем параметры
Getopt::Long::Configure ("bundling");
GetOptions ('c'      => \$opt_c,
            'd'      => \$opt_d,
            'a'      => \$opt_a,
            'root=s' => sub{($opt_root = $_[1]) =~ s/\/+$//;},
           ) or exit;
           
# Каталоги, в которые будут загружаться фото и видео файлы
my %folder = (
'jpg' => $opt_root . '/Мои фотки',
'avi' => $opt_root . '/Мое видео',
'3gp' => $opt_root . '/Мое видео',
'mp4' => $opt_root . '/Мое видео',
);

# Типы файлов
my $type = join '|', map {"($_)"} keys %folder;

# Читаем файлы
while (<STDIN>) {
    chomp;

    # Обработка файла
    if ($_ =~ /$type$/i) {
        print "$_ -> ";

        if (-e $_) {
            # Получить данные файла
            my ($exp, $dev, $path, $file) = Imover::get($_, $model);
            
            print "$folder{$exp}/$dev $path/$file\n";

            # Скопировать файл
            if($opt_c) {
                # Переместить файл
                Imover::mov($_, $folder{$exp}, $dev, $path, $file);

                # Удалить оригинал
                if ($opt_d) {
                    unlink($_) || die "Невозможно удалить файл $_: $!";
                };
            };
        }
        else {
            print "Ошибка: файл не найден\n";
        };
    }
    else {
        if ($opt_a) {
            print "$_ -> Неподдерживаемый тип файла\n";
        };
    };
};
